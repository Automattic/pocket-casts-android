import io.sentry.android.gradle.extensions.InstrumentationFeature

plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.kapt
    alias libs.plugins.ksp
    alias libs.plugins.hilt
    alias libs.plugins.sentry
    alias libs.plugins.google.services
    alias libs.plugins.kotlin.parcelize
}

apply from: "../base.gradle"
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    namespace 'au.com.shiftyjelly.pocketcasts'

    defaultConfig {
        applicationId project.applicationId
        multiDexEnabled true
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$rootDir/modules/services/model/schemas".toString())
    }

    buildFeatures {
        buildConfig true
        viewBinding true
        dataBinding = true
        compose true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'

            def gitLongVersion = "git -C ${rootDir} describe --tags --long --dirty".execute().text.trim()
            def (gitTag, versionBuild, gitHash, gitDirty) = gitLongVersion.tokenize('-')

            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_radioactive",
                    gitHash: gitHash + (gitDirty ? ("-" + gitDirty) : ""),
                    sentryDsn: ""
            ]
        }

        debugProd {
            initWith debug
            manifestPlaceholders += [
                    appIcon: "@mipmap/ic_launcher_3",
            ]
        }

        release {
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    gitHash: "",
                    sentryDsn: project.pocketcastsSentryDsn
            ]

            if (!project.pocketcastsSentryDsn) {
                println "WARNING: Sentry DSN gradle property 'pocketcastsSentryDsn' not found. Crash reporting won't work without this."
            }

            if (!file("${project.rootDir}/sentry.properties").exists()) {
                println "WARNING: Sentry configuration file 'sentry.properties' not found. The ProGuard mapping files won't be uploaded."
            }

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
        }
    }
}

sentry {
    includeProguardMapping = file("$rootDir/sentry.properties").exists()
    tracingInstrumentation {
        features = EnumSet.allOf(InstrumentationFeature) - InstrumentationFeature.FILE_IO
  }
}

dependencies {
    // features
    implementation project(':modules:features:account')
    implementation project(':modules:features:discover')
    implementation project(':modules:features:endofyear')
    implementation project(':modules:features:filters')
    implementation project(':modules:features:navigation')
    implementation project(':modules:features:player')
    implementation project(':modules:features:podcasts')
    implementation project(':modules:features:profile')
    implementation project(':modules:features:search')
    implementation project(':modules:features:settings')
    implementation project(':modules:features:shared')
    implementation project(':modules:features:taskerplugin')
    // services
    implementation project(':modules:services:analytics')
    implementation project(':modules:services:compose')
    implementation project(':modules:services:localization')
    implementation project(':modules:services:model')
    implementation project(':modules:services:preferences')
    implementation project(':modules:services:repositories')
    implementation project(':modules:services:servers')
    implementation project(':modules:services:utils')
    implementation project(':modules:services:ui')
    implementation project(':modules:services:views')
    testImplementation project(':modules:services:sharedtest')
}

task appStart(type: Exec, dependsOn: 'installDebug') {
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'au.com.shiftyjelly.pocketcasts.debug/au.com.shiftyjelly.pocketcasts.ui.MainActivity'
}

task useGoogleServicesDebugFile {
    description 'Copies the debug google-services.json file if file is missing.'
    doLast {
        def googleServicesFile = "google-services.json"
        if (!file("${project.projectDir}/$googleServicesFile").exists()) {
            def debugOnlyFile = "google-services.json_debug-only"
            println "$googleServicesFile file is missing. Copying $debugOnlyFile"
            copy {
                from "${project.projectDir}/$debugOnlyFile"
                into project.projectDir
                rename { googleServicesFile }
            }
        }
    }
}
preBuild.dependsOn(useGoogleServicesDebugFile)
