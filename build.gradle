// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlin_version = '1.7.10'
        hilt_version = '2.43.2'
    }
    repositories {
        google()
        mavenLocal()

        maven {
            url 'https://a8c-libs.s3.amazonaws.com/android' 
            content {
                includeGroup "com.automattic.android"
                includeGroup "com.automattic.android.publish-to-s3"
            }
        }
    }

    // Gradle Plugins
    dependencies {
        // Google Services - https://developers.google.com/android/guides/google-services-plugin
        classpath libs.google.services
        // Open source licenses plugin
        classpath libs.oss.licenses
    }

    // Force proguard to the new version to fix "proguard.ParseException: Use of generics not allowed for java type at '<1>_<2>_<3>JsonAdapter' in line 31 of file '/Users/philip/.gradle/caches/transforms-2/files-2.1/d5ca2d039e1d32e9ba6bfd6a67df5151/META-INF/proguard/moshi.pro'"
    configurations.all {
        resolutionStrategy {
            force 'net.sf.proguard:proguard-gradle:6.1.1'
        }
    }
}

plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.dagger.hilt.android) apply false
    alias(libs.plugins.mikepenz.aboutlibraries) apply false
    alias(libs.plugins.ben.manes.versions) apply false
    alias(libs.plugins.diffplug.spotless) apply false
    alias(libs.plugins.sentry.android) apply false
}

apply plugin: 'base'
apply from: rootProject.file('dependencies.gradle')
apply from: 'scripts/git-hooks/install.gradle'

apply plugin: 'com.diffplug.spotless'
spotless {
    kotlin {
        target '**/*.kt'
        targetExclude("$buildDir/**/*.kt")
        targetExclude('bin/**/*.kt')
        ktlint('0.45.1')
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://a8c-libs.s3.amazonaws.com/android"
            content {
                includeGroup "com.automattic"
                includeGroup "com.automattic.tracks"
            }
        }
    }
}

