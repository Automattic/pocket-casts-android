final VERSION_CODE_DIFFERENCE_BETWEEN_APP_AND_AUTOMOTIVE = 50000
final VERSION_CODE_DIFFERENCE_BETWEEN_APP_AND_WEAR = 50000 + VERSION_CODE_DIFFERENCE_BETWEEN_APP_AND_AUTOMOTIVE

def secretProperties = loadPropertiesFromFile(file("$rootDir/secret.properties") )
def versionProperties = loadPropertiesFromFile(file("${rootDir}/version.properties"))

static def loadPropertiesFromFile(inputFile) {
    def properties = new Properties()
    if (inputFile.exists()) {
        inputFile.withInputStream { stream ->
            properties.load(stream)
        }
    }
    return properties
}

// Unfortunately we currently need to rely on a property to separate the version code between app & automotive builds.
// This is mainly because the library modules use the version code information from BuildConfig directly instead
// of receiving from the clients.
//
// On the bright side, the difference between the app & automotive modules is a static number, so even if there is
// an issue and the wrong version code information is used, it'd be easy to identify it.
//
// The long term solution is to move the version information out of the `base.gradle` and into the specific
// app & automotive modules and pass this information to the modules that require it.
def isAutomotiveBuild = (project.findProperty("IS_AUTOMOTIVE_BUILD") ?: false).toBoolean()
def isWearBuild = (project.findProperty("IS_WEAR_BUILD") ?: false).toBoolean()
def getVersionCode = {
    def appVersionCode = versionProperties.getProperty("versionCode", null).toInteger()
    if (isAutomotiveBuild) {
        return appVersionCode + VERSION_CODE_DIFFERENCE_BETWEEN_APP_AND_AUTOMOTIVE
    } else if (isWearBuild) {
       return appVersionCode + VERSION_CODE_DIFFERENCE_BETWEEN_APP_AND_WEAR
    }
    return appVersionCode
}
def getVersionName = {
    def versionName = versionProperties.getProperty("versionName", null)
    if (isAutomotiveBuild) {
        return "${versionName}a"
    } else if (isWearBuild) {
        return "${versionName}w"
    }
    return versionName
}

project.ext {
    // Application
    applicationId = 'au.com.shiftyjelly.pocketcasts'

    versionName = getVersionName()
    versionCode = getVersionCode()

    // Android
    minSdkVersion = 23
    minSdkVersionWear = 26
    targetSdkVersion = 33
    compileSdkVersion = 33
    testInstrumentationRunner = 'androidx.test.runner.AndroidJUnitRunner'

    // App Signing
    storeFile = file("$rootDir/${secretProperties.getProperty("signingKeyStoreFile", null)}")
    if (storeFile.exists()) {
        // Use secret properties
        storePassword = secretProperties.getProperty("signingKeyStorePassword", null)
        keyAlias = secretProperties.getProperty("signingKeyAlias", null)
        keyPassword = secretProperties.getProperty("signingKeyPassword", null)
    } else {
        // Check local gradle properties
        def keystoreFilePropertyKey = "pocketcastsKeyStoreFile"
        if (project.hasProperty(keystoreFilePropertyKey)) {
            storeFile = file(project.getProperty(keystoreFilePropertyKey))
            storePassword = project.getProperty("pocketcastsKeyStorePassword")
            keyAlias = project.getProperty("pocketcastsKeyStoreAlias")
            keyPassword = project.getProperty("pocketcastsKeyStoreAliasPassword")
        }
    }
    canSignRelease = storeFile.exists()

    // Secrets
    settingsEncryptSecret = secretProperties.getProperty("pocketcastsSettingsEncryptSecret", "")
    sharingServerSecret = secretProperties.getProperty("pocketcastsSharingServerSecret", "")
    pocketcastsSentryDsn = secretProperties.getProperty("pocketcastsSentryDsn", "")
    googleSignInServerClientId = secretProperties.getProperty("googleSignInServerClientId", "")
}
