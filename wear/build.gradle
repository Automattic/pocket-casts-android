import io.sentry.android.gradle.extensions.InstrumentationFeature

plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.kapt
    alias libs.plugins.ksp
    alias libs.plugins.hilt
    alias libs.plugins.sentry
    alias libs.plugins.google.services
    alias libs.plugins.kotlin.parcelize
}

apply from: "../base.gradle"

android {
    namespace 'au.com.shiftyjelly.pocketcasts'

    defaultConfig {
        minSdk project.minSdkVersionWear
        applicationId project.applicationId
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'

            def gitLongVersion = "git -C ${rootDir} describe --tags --long --dirty".execute().text.trim()
            def (gitTag, versionBuild, gitHash, gitDirty) = gitLongVersion.tokenize('-')

            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_radioactive",
                    gitHash: gitHash + (gitDirty ? ("-" + gitDirty) : ""),
                    sentryDsn: ""
            ]
        }

        debugProd {
            initWith debug
        }

        release {
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    gitHash: "",
                    sentryDsn: project.pocketcastsSentryDsn
            ]

            if (!project.pocketcastsSentryDsn) {
                println "WARNING: Sentry DSN gradle property 'pocketcastsSentryDsn' not found. Crash reporting won't work without this."
            }

            if (!file("${project.rootDir}/sentry.properties").exists()) {
                println "WARNING: Sentry configuration file 'sentry.properties' not found. The ProGuard mapping files won't be uploaded."
            }

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
        }
    }

    buildFeatures {
        buildConfig true
        viewBinding true
        dataBinding = true
        compose true
    }

    kotlinOptions {
        jvmTarget = "1.8"
        // Allow for widescale experimental APIs in Alpha libraries we build upon
        freeCompilerArgs += "-opt-in=com.google.android.horologist.annotations.ExperimentalHorologistApi"
        freeCompilerArgs += "-opt-in=com.google.android.horologist.media.ui.ExperimentalHorologistMediaUiApi"
    }
}

sentry {
    includeProguardMapping = file("$rootDir/sentry.properties").exists()
    tracingInstrumentation {
        features = EnumSet.allOf(InstrumentationFeature) - InstrumentationFeature.FILE_IO
    }
}

dependencies {
    implementation androidLibs.wearInput
    implementation androidLibs.wearRemoteInteractions

    // General Compose dependencies
    implementation androidLibs.composeActivity
    implementation androidLibs.composeUi
    implementation androidLibs.composeMaterialIconsExtended
    implementation androidLibs.composeUiToolingPreview
    implementation androidLibs.hiltNavigationCompose

    // Compose for Wear OS Dependencies
    implementation androidLibs.wearComposeMaterial
    implementation androidLibs.wearComposeFoundation
    implementation androidLibs.wearComposeNavigation

    // Horologist Dependencies
    implementation androidLibs.horologistAudioUi
    implementation androidLibs.horologistAuthData
    implementation androidLibs.horologistAuthComposables
    implementation androidLibs.horologistAuthUi
    implementation androidLibs.horologistComposeMaterial
    implementation androidLibs.horologistDatalayer
    implementation androidLibs.horologistMedia
    implementation androidLibs.horologistMedia3Backend
    implementation androidLibs.horologistMediaData
    implementation androidLibs.horologistMediaUi
    implementation androidLibs.horologistNetworkAwarnessOkHttp

    implementation androidLibs.media3DatasourceOkhttp

    implementation androidLibs.horologistComposeLayout

    implementation project(':modules:services:analytics')
    implementation project(':modules:services:compose')
    implementation project(':modules:services:images')
    implementation project(':modules:services:localization')
    implementation project(':modules:services:model')
    implementation project(':modules:services:preferences')
    implementation project(':modules:services:repositories')
    implementation project(':modules:services:servers')
    implementation project(':modules:services:ui')
    implementation project(':modules:services:utils')
    testImplementation project(':modules:services:sharedtest')

    implementation project(':modules:features:account')
    implementation project(':modules:features:podcasts')
    implementation project(':modules:features:profile')
    implementation project(':modules:features:settings')
    implementation project(':modules:features:shared')
}

task useGoogleServicesDebugFile {
    description 'Copies the debug google-services.json file if file is missing.'
    doLast {
        def googleServicesFile = "google-services.json"
        if (!file("${project.projectDir}/$googleServicesFile").exists()) {
            def debugOnlyFile = "google-services.json_debug-only"
            println "$googleServicesFile file is missing. Copying $debugOnlyFile"
            copy {
                from "${project.projectDir}/$debugOnlyFile"
                into project.projectDir
                rename { googleServicesFile }
            }
        }
    }
}
preBuild.dependsOn(useGoogleServicesDebugFile)
